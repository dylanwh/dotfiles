#!/usr/bin/env perl
use strict;
use warnings;
use File::stat;
use IO::Handle;
use Fcntl 'F_GETFD', 'F_SETFD', 'FD_CLOEXEC';
use Getopt::Long qw(  :config pass_through gnu_compat bundling no_getopt_compat );
use YAML::XS 'LoadFile', 'Dump';
use if $ENV{DISPLAY}, 'Clipboard';

my ($root);
GetOptions(
    'root|r' => \$root,
);

my $name       = shift @ARGV;
my $hosts_file = "$ENV{HOME}/.sshfu";

die "config file has dangerous perms" if stat($hosts_file)->mode & 0066;
my @hosts = @{ LoadFile($hosts_file) };

my @matches = grep { match($_, $name)  } @hosts;

die "No hosts match $name\n"       if @matches < 1;
die "Too many hosts match $name\n" if @matches > 1;

my $host = $matches[0];

print "\e]2;sshfu: $host->{name}\a";
sshpass($host, @ARGV);

sub sshpass {
    my ($host, @args) = @_;
    my $user = $root ? 'root' : $host->{admin};
    my $pass = $root ? $host->{root_pass} : $host->{admin_pass};

    my ($in, $out);
    pipe ($in, $out);
    $out->autoflush(1);
    $out->print($pass . "\n");

    my $flags = fcntl($in, F_GETFD, 0);
    fcntl($in, F_SETFD, $flags ^ FD_CLOEXEC);
    my $fd = $in->fileno;

    my $address = $host->{ip_address} // $host->{hostname};
    print "Going to ssh to $user\@$address, using fd $fd for password (args: @args)\n";
    if ($ENV{DISPLAY}) {
        print "Root password in clipboard.\n";
        Clipboard->copy($host->{root_pass});
    }
    else {
        print "Root password is $host->{root_pass}\n";
    }
    exec('sshpass', "-d$fd", 'ssh', '-oStrictHostKeyChecking=no', '-A', "$user\@$address");
}

sub match {
    my ($host, $name) = @_;

    index($host->{name}, $name) >= 0
        or index($host->{hostname}, $name) >= 0;
}
