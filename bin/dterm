#!/usr/bin/env perl
use strict;
use warnings;
use File::Basename;
use Getopt::Long qw( :config gnu_getopt );

my $TERM  = 'urxvt';
my $SHELL = $ENV{SHELL} || '/bin/bash';
my ($title, $name, $dtach, $host, $raw, $eep, $ctrl_l);

foreach (@ARGV) {
    if ($_ eq '-e') {
        $_ = '--';
        last 
    }
    elsif (/^-([rd]+)e$/ or /^-e(.+)$/) {
        unshift @ARGV, "-$1" if $1;
        $_ = '--';
        last;
    } 
}


GetOptions(
    'title|t=s' => \$title,
    'name|n=s'  => \$name,
    'dtach|d'   => \$dtach,
    'host|h=s'  => \$host,
    'raw|r'     => \$raw,
    'l'         => \$ctrl_l,
    'e'         => \$eep,
);

my (@run) = @ARGV;

if (not $title and @run) {
    $title = "$run[0]: @run[1..$#run]";
}

if (not $name and $run[0]) {
    $name = $run[0];
    $name =~ s/\W+/_/g;
}

if ($dtach) {
    my $socket = $name || "dterm.$$";
    mkdir "$ENV{HOME}/.dtach";
    my @dtach = (
        'dtach',
        '-A'  => "$ENV{HOME}/.dtach/$socket",
        '-r'  => $ctrl_l ? 'ctrl_l' : 'winch',
        '-Ez',
        @run ? () : ($SHELL),
    );
    unshift @run, @dtach;
}

if (not $raw) {
    unshift @run, '-e' if @run;
    $name = 'outside'  if -e "$ENV{HOME}/.outside";

    exec($TERM =>
        -ls =>
        option(-name => $name),
        option(-title => $title), 
        @run);
}
else {
    exec(@run);
}

sub option {
    my ($key, $val) = @_;
    return ($key, $val) if $val;
    return ();
}
