#!/usr/bin/env perl
use feature 'say';
use strict;
use warnings;
use Env;
use Env qw(@PATH);
use File::Path qw(mkpath);
use File::Basename qw(dirname basename);
use File::Spec::Functions qw(catdir rel2abs abs2rel);
use File::stat qw(lstat);
use IO::File;
use Cwd qw(realpath);
use autodie qw(:default);

my $REPO = realpath(catdir(dirname(realpath(__FILE__)), '..'));

provide('fish',          '.config/fish');
provide('selenized',     '.config/selenized');
provide('starship.toml', '.config/starship.toml');
provide('imapfilter',    '.imapfilter');
provide('terminfo',      '.terminfo');
provide('iterm2',        '.iterm2');
provide('vim',           '.vim');
provide('vim',           '.config/nvim');
provide('mutt',          '.mutt');
provide('doom',          '.doom.d');

my @dotfiles = (
  'ctags',         'cvsignore',
  'fdignore',      'iterm2_shell_integration.fish',
  'mailcap',       'minicpanrc',
  'msmtprc',       'offlineimap.py',
  'offlineimaprc', 'quotes',
  'reply.pl',      'screenrc',
  'tmux.conf',     'todorc',
  'wyrdrc'
);

provide($_, ".$_") for @dotfiles;

mkpath "$HOME/.local/go";

if (-l "$HOME/.local/bin") {
  unlink "$HOME/.local/bin";
}
mkpath "$HOME/.local/bin";

foreach my $file (glob "$REPO/bin/*") {
  my $repo_file = abs2rel($file,                                      $REPO);
  my $home_file = abs2rel("$HOME/.local/bin/" . basename($repo_file), $HOME);

  provide($repo_file, $home_file);
}

if (-d "$HOME/bin") {
  rename("$HOME/bin", "$HOME/bin.civil-war-backup");
}
if (-d "$HOME/go") {
  rename("$HOME/go", "$HOME/go.civil-war-backup");
}
if (-f "$HOME/.vimrc") {
  rename("$HOME/.vimrc", "$HOME/.vimrc.civil-war-backup");
}
if (-d "$HOME/.git") {
  rename("$HOME/.git", "$HOME/.oldgit");
}

mkpath "$HOME/.ssh";
if (-f "$HOME/Sync/ssh/authorized_keys") {
  provide("$HOME/Sync/ssh/authorized_keys", ".ssh/authorized_keys");
}
else {
  system('ssh-import-id', 'gh:dylanwh');
}
open my $fh, '>', "$HOME/.ssh/config";
print $fh "Include $REPO/ssh/config.d/*\n";
close $fh;

sub provide {
  my ($source, $target) = @_;
  $target //= $source;
  my $s = rel2abs($source, $REPO);
  my $t = rel2abs($target, $HOME);
  mkpath(dirname($t));
  my $stat = lstat $t;
  if ($stat) {
    if (-l $stat) {
      unless (readlink($t) eq $s) {
        printf "%s: %s -> %s\n", $t, readlink($t), $s;
        unlink($t);
        symlink($s, $t);
      }
    }
    elsif (-f $stat) {
      printf "%s: conflicting file\n", $t;
      rename($t, "$t.civil-war-backup");
      symlink($s, $t);
    }
    elsif (-d $stat) {
      printf "%s: conflicting directory\n", $t;
      rename($t, "$t.civil-war-backup");
      symlink($s, $t);
    }
    else {
      printf "%s: unknown type\n", $t;
    }
  }
  else {
    printf "%s: new file or directory\n", $t;
    symlink($s, $t);
  }
}

