#!/usr/bin/env perl
use strict;
use warnings;
use autodie;
use Path::Class;
use IO::Pipe;
use File::HomeDir;
use DateTime;
use DateTime::Format::Strptime;

my $root = dir( File::HomeDir::FreeDesktop->my_download );
print "dir: $root\n";
my $fmt  = new DateTime::Format::Strptime( pattern => '%Y/%m/%d' );

while ( my $file = $root->next ) {
    next if $file->is_dir;

    my $mtime     = $fmt->format_datetime( DateTime->from_epoch( epoch => $file->stat->mtime ) );
    my $mime_type = get_mime($file . "");
    my $date_dir  = $root->subdir($mtime);
    my $mime_dir  = $root->subdir('mime')->subdir($mime_type);

    $date_dir->mkpath;
    $mime_dir->mkpath;

    rename( $file, $date_dir->file( $file->basename ) );
    symlink( $date_dir->file( $file->basename ), $mime_dir->file( $file->basename ) ) 
        unless -e $mime_dir->file( $file->basename );
}

my $today = $root->subdir( $fmt->format_datetime( DateTime->now ) );
$today->mkpath;

unlink($root->subdir('today')) if -e $root->subdir('today');
symlink($today, $root->subdir('today'));

sub get_mime {
    my $file = shift;
    my $pipe = IO::Pipe->new;
    $pipe->reader("file", qw( -bL --mime-type ), $file);
    my $mime = $pipe->getline;
    chomp $mime;

    return $mime;
}

