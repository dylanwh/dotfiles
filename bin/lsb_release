#!/usr/bin/perl
use strict;
use warnings;
use YAML;
use File::Slurp;
use List::MoreUtils 'any';
use Sys::Hostname;
use constant DEBUG => $ENV{DEBUG} || 0;

my $parent         = getppid();
my $parent_exe     = readlink "/proc/$parent/exe";
my $parent_cmdline = read_file("/proc/$parent/cmdline");
my $is_chrome      = $parent_exe =~ /google.+chrome/ && any {/^-d/} @ARGV;

if (DEBUG) {
    open my $fh, ">>$ENV{HOME}/lsb_release.log";
    print $fh Dump {
        parent => {
            pid       => $parent,
            exe       => $parent_exe,
            cmdline   => $parent_cmdline,
            is_chrome => $is_chrome,
        },
        argv     => [@ARGV],
        hostname => hostname(),
    };
    close $fh;
}

if ($is_chrome) {
    my $hostname = hostname();
    print "Description:\t$hostname\n";
}
else {
    exec("/usr/bin/lsb_release", @ARGV);
}
