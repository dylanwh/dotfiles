#!/bin/zsh

PROFILE_DIR=$HOME/.screenlayout

profile=$1

function die {
    echo "$@" >&2
    exit 1
}

before_hooks=( $PROFILE_DIR/before/*.sh(N) )
after_hooks=( $PROFILE_DIR/after/*.sh(N) )

function before_hooks {
    for hook in $before_hooks; do
        $hook $profile
    done
}

function after_hooks {
        for hook in $after_hooks; do
            $hook $profile
        done
    }

if [[ -n $profile ]]; then
    profile_script=$PROFILE_DIR/$profile@$HOST.sh

    if [[ -x $profile_script ]]; then

        before_hooks
        $profile_script
        profile_status=$?
        after_hooks

        if [[ -L $profile_script ]]; then
            current_target=$(basename $(readlink $profile_script))
        else
            current_target=$(basename $profile_script)
        fi

        if (( $profile_status == 0 )); then
            if [[ -f $PROFILE_DIR/current@$HOST.sh ]]; then
                mv $PROFILE_DIR/current@$HOST.sh $PROFILE_DIR/last@$HOST.sh
            fi
            ( cd $PROFILE_DIR && ln -fs $current_target current@$HOST.sh )
        fi
        exit $profile_status
    else
        before_hooks
        die "no such layout" >&2
        after_hooks
    fi
else
    profiles=( $PROFILE_DIR/*@$HOST.sh(.:t:r:s/@$HOST/) )
    (( $#profiles == 0 )) && die "no layouts for $HOST"

    for profile in $profiles; do
        echo $profile
    done | sort
fi
