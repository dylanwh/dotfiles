#!/bin/zsh

pubdir=$(xdg-user-dir PUBLICSHARE)
suffix=''
name=''
file=''

function newfile() {
	if [[ -z $name ]]; then
		TMPDIR="$pubdir" tempfile -d "$pubdir" -p clip_ -s ${suffix:-.txt}
	else
		echo "$pubdir/$name$suffix"
	fi
}

if [[ -f $1 ]]; then
	argv[1]=(-f $1)
fi

while getopts "d:n:s:f:" opt; do
	case $opt in
		d) pubdir="$pubdir/$OPTARG" ;;
		n) name=$OPTARG             ;;
		s) suffix=$OPTARG           ;;
		f)
			file=$OPTARG
			suffix=${suffix:-.${file:e}}
			name=${name:-${file:t:r}}
		;;
		'?')
			exit 1
		;;
	esac
done

pubfile=$(newfile)
if [[ -n $file ]]; then
	cp $file $pubfile
else
	> $pubfile
fi

until dropbox filestatus $pubfile | grep -Fq "$pubfile: up to date"
do
	sleep .1
done

dropbox puburl $pubfile
