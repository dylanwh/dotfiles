#!/bin/zsh

pubdir=$(xdg-user-dir PUBLICSHARE)
suffix=''
name=''
file=''
integer verbose=0

function newfile() {
	if [[ -z $name ]]; then
		TMPDIR="$pubdir" tempfile -d "$pubdir" -p clip_ -s ${suffix:-.txt}
	else
		echo "$pubdir/$name$suffix"
	fi
}

if [[ -f $1 ]]; then
	argv[1]=(-f $1)
fi

while getopts "d:n:s:f:v" opt; do
	case $opt in
		d) pubdir="$pubdir/$OPTARG" ;;
		n) name=$OPTARG             ;;
		s) suffix=$OPTARG           ;;
		f)
			file=$OPTARG
			suffix=${suffix:-.${file:e}}
			name=${name:-${file:t:r}}
		;;
		v)
			let verbose=1
		;;
		'?')
			exit 1
		;;
	esac
done

pubfile=$(newfile)
if [[ -n $file ]]; then
	cp $file $pubfile
else
	> $pubfile
fi

integer count=1
until dropbox filestatus $pubfile | grep -Fq "$pubfile: up to date"
do
	(( verbose && count++ % 10 == 0 )) && dropbox status >&2
	sleep .1
done

dropbox puburl $pubfile
