#!/usr/bin/env perl
use strict;
use warnings;
use Path::Class 'dir', 'file';
use List::MoreUtils 'all', 'none';
use Set::Object 'set';

my @special = qw( cur new tmp );
my $root = dir( shift || '.' );
my @dirs = ($root);
my @mailboxes;
my $seen = set();

while (my $dir = pop @dirs) {
    next if $seen->contains($dir->stringify);
    $seen->insert($dir->stringify);

    while (my $entry = $dir->next) {
        next if $entry eq $dir;
        next if $entry eq $dir->subdir('..');
        next if $entry eq $dir->subdir('archive');
        next if $entry eq $dir->subdir('sent');

        if ($entry->is_dir) {
            my $basename = file($entry)->basename;
            push @dirs, $entry if none { $basename eq $_ } @special;
            if (all { -d $entry->subdir($_) } @special) {
                my $prefix = quotemeta($root->stringify);
                my $mailbox = $entry->stringify;
                $mailbox =~ s!^$prefix/!+!;
                push @mailboxes, $mailbox;
            }
        }
    }
}

@mailboxes = grep !/inbox$/, sort @mailboxes;
print "mailboxes +inbox @mailboxes\n";
