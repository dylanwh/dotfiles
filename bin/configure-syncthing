#!/usr/bin/env perl
use 5.30.2;
use Env;
use English;
use Mojo::DOM;
use Mojo::File qw(path);
use Sys::Hostname;

chdir $HOME;

my $SYNCTHING
  = $OSNAME eq 'darwin'
  ? path($HOME, 'Library', 'Application Support', 'Syncthing')
  : path($HOME, '.config', 'syncthing');

my $name        = (split(/\./, hostname()))[0];
my $user        = $LOGNAME || 'dylan';
my $hostname    = "$name.dur.me";
my $password    = path("$HOME/.st_password_hash")->slurp;
my $config_file = $SYNCTHING->child("config.xml");
my $backup_file = $SYNCTHING->child("config.xml~");

chomp $password;

my $xml_prefix = qq{<?xml version="1.0"?>\n};

if ($OSNAME eq 'linux') {
  system("sudo systemctl stop syncthing\@$user");
}
elsif ($OSNAME eq 'darwin') {
  system("launchctl unload  Library/LaunchAgents/syncthing.plist");
}

unless (-f $backup_file) {
  $config_file->copy($backup_file);
}

my $input = $config_file->slurp;
my $dom   = Mojo::DOM->new("$xml_prefix$input");

$dom->find('gui user')->map('remove');
$dom->find('gui password')->map('remove');
$dom->at('gui')->append_content($dom->new_tag(user     => $user));
$dom->at('gui')->append_content($dom->new_tag(password => $password));

if ($OSNAME eq 'darwin') {
  $dom->at('gui address')->content("127.0.0.1:8384");
}
else {
  $dom->at('gui address')->content("0.0.0.0:8384");
}


$dom->find('options listenAddress')->map('remove');
$dom->at('options')
  ->prepend_content($dom->new_tag(listenAddress => "default"));
# $dom->at('options')
#   ->prepend_content( $dom->new_tag(listenAddress => "dynamic+https://relays.syncthing.net/endpoint"));
$dom->at('options globalAnnounceEnabled')->content('false');
$dom->at('options localAnnounceEnabled')->content('true');
$dom->at('options relaysEnabled')->content('false');
$dom->at('options natEnabled')->content('false');
$dom->at('options startBrowser')->content('false');

#$dom->at("device[name='$name'] address")->content("$hostname");

say "devices: ";
$dom->find("device[name]")->each(sub {
  my $name = $_->attr('name');
  $_->find('address')->map('remove');
  $_->append_content($dom->new_tag(address => "quic://$name.hardison.net:22000"));
  $_->append_content($dom->new_tag(address => "tcp://$name.hardison.net:22000"));
});

$config_file->spurt(substr($dom->to_string, length($xml_prefix)));

if ($OSNAME eq 'linux') {
  system("sudo systemctl start syncthing\@$user");
}
elsif ($OSNAME eq 'darwin') {
  system("launchctl load  Library/LaunchAgents/syncthing.plist");
}
