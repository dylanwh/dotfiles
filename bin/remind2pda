#!/usr/bin/env perl
use strict;
use warnings;
use YAML 'LoadFile';
use Time::Piece;
use Time::Seconds;
use Palm::Datebook;

my $pdb = new Palm::Datebook;
my $pid = open my $remind, '-|', 'rem', '-s12', '-ipda=1' or die "cannot exec rem: $!";
my %seen;
my @log;
my $total = 0;

while ($_ = readline $remind) {
	chomp;
	my ($date, $special, $tag, $duration, $time, $body) = split(/\s+/, $_, 6);
	my ($year, $month, $day) = split(/\//, $date);
	$body =~ s/^(\d\d?:\d\d(pm|am)?-)?\d\d?:\d\d(am|pm) //;
	my $id = '*';


	if ($tag =~ /@([\w.-]+)/) {
		$id = $1;
	}

	my $record = $pdb->new_Record;
	$record->{day} = $day;
	$record->{month} = $month;
	$record->{year} = $year;

	push @log, [ $id, $body ];

	if ($time ne '*') {
		my $start = Time::Piece->strptime(
			"$date " . timefrob($time), "%Y/%m/%d %H:%M"
		);
		$record->{start_hour} = $start->hour;
		$record->{start_minute} = $start->minute;
		if ($duration eq '*') {
			$duration = 10;
		}

		$duration = $duration + 0;
		my $end = $start + (ONE_MINUTE * $duration);
		$record->{end_hour} = $end->hour;
		$record->{end_minute} = $end->minute;
	}

	delete $record->{alarm};

	$record->{description} = $body;
	$pdb->append_Record($record);

	$total++;
}

my $record = $pdb->new_Record();
$record->{day} = 1;
$record->{month} = 1;
$record->{year} = 2000;
$record->{description} = "LOG";
$record->{note} = join("\n", map(join(' ', @$_), @log));
$pdb->append_Record($record);

$pdb->Write('DatebookDB.pdb');
close $remind;
print "Added $total calendar entries.\n";


sub timefrob {
	my $n = shift;
	return $n if $n eq '*';
	int($n / 60) . ":" . ($n % 60);
}
