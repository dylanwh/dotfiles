#!/usr/bin/env perl
use feature 'say';
use strict;
use warnings;
use Env;
use File::Path qw(mkpath rmtree);
use File::Basename qw(dirname);
use File::Spec::Functions qw(catdir rel2abs);
use File::stat qw(lstat);
use IO::File;
use Cwd qw(realpath);
use autodie qw(:default);

my %seen;
while (my $git_dir = <ARGV>) {
  chomp $git_dir;
  my $dir = realpath(dirname($git_dir));
  my $origin = qx(git -C $dir remote get-url origin);
  my $matches = $origin =~ m{
    ^
    (?: (?<protocol> https? | git )://
      | (?: (?<user> [^@]+?)@)? )
    (?<host> [a-zA-Z0-9.-]+)
    [:/]
    (?<path> .+?)
    (?:\.git)?
    $
  }xs;
  my %origin = %+;
  if ($matches) {
    my $root = $origin{host} =~ /github\.com/i ? "Git" : "Git/$origin{host}";
    my $new_dir = catdir($HOME, $root, $origin{path});
    next if $dir eq $new_dir;
    if (-d $new_dir) {
      my $i = 2;
      do {
        $new_dir = catdir($HOME, $root, $origin{path} . '_' . $i++);
      } while -d $new_dir;
    }
    say "mkdir -p ", dirname($new_dir) unless -d dirname($new_dir);
    say "mv $dir $new_dir";
  }
}

